plugins {
    id 'fabric-loom' version '1.1-SNAPSHOT'
    id 'maven-publish'
    id 'java'
}

version = project.mod_version
group = project.maven_group

sourceCompatibility = jdk_version
targetCompatibility = jdk_version

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
}

dependencies {
    compileOnly "org.projectlombok:lombok:${project.lombok_version}"
    annotationProcessor "org.projectlombok:lombok:${project.lombok_version}"

    // To change the versions see the gradle.properties file
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    // Fabric API. This is technically optional, but you probably want it anyway.
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

    include modImplementation("com.github.sib-energy-craft:energy-api:${project.sec_energy_api_version}")
    include modImplementation("com.github.sib-energy-craft:sec-utils:${project.sec_utils_version}")

    include modImplementation("com.github.sib-energy-craft:energy-containers-api:${project.sec_energy_containers_api_version}")
    include modImplementation("com.github.sib-energy-craft:energy-machines-api:${project.sec_energy_machines_api_version}")

    include modImplementation("com.github.sib-energy-craft:mod-iron-craft-table-api:${project.sec_mod_iron_craft_table_api_version}")
    include modImplementation("com.github.sib-energy-craft:mod-tools-api:${project.sec_mod_tools_api_version}")
    include modImplementation("com.github.sib-energy-craft:mod-pipes-api:${project.sec_mod_pipes_api_version}")

    include modImplementation("com.github.sib-energy-craft:mod-tools-impl:${project.sec_mod_tools_impl_version}")
    include modImplementation("com.github.sib-energy-craft:mod-rubber:${project.sec_mod_rubber_version}")
    include modImplementation("com.github.sib-energy-craft:mod-recipes:${project.sec_mod_recipes_version}")
    include modImplementation("com.github.sib-energy-craft:mod-recipes-impl:${project.sec_mod_recipes_impl_version}")
    include modImplementation("com.github.sib-energy-craft:mod-ores:${project.sec_mod_ores_version}")
    include modImplementation("com.github.sib-energy-craft:mod-metallurgy:${project.sec_mod_metallurgy_version}")
    include modImplementation("com.github.sib-energy-craft:mod-cables:${project.sec_mod_cables_version}")
    include modImplementation("com.github.sib-energy-craft:mod-energy-items:${project.sec_mod_energy_items_version}")
    include modImplementation("com.github.sib-energy-craft:mod-machines:${project.sec_mod_machines_version}")
    include modImplementation("com.github.sib-energy-craft:mod-solar-panels:${project.sec_mod_solar_panels_version}")
    include modImplementation("com.github.sib-energy-craft:mod-energy-tools:${project.sec_mod_energy_tools_version}")
    include modImplementation("com.github.sib-energy-craft:mod-chests:${project.sec_mod_chests_version}")
    include modImplementation("com.github.sib-energy-craft:mod-pipes:${project.sec_mod_pipes_version}")
    include modImplementation("com.github.sib-energy-craft:mod-quarry:${project.sec_mod_quarry_version}")
    include modImplementation("com.github.sib-energy-craft:mod-agronomy:${project.sec_mod_agronomy_version}")
    include modImplementation("com.github.sib-energy-craft:mod-bronze-age:${project.sec_mod_bronze_age_version}")
}

processResources {
    inputs.property "version", project.version
    filteringCharset "UTF-8"

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

def targetJavaVersion = Integer.valueOf(jdk_version)
tasks.withType(JavaCompile).configureEach {
    // ensure that the encoding is set to UTF-8, no matter what the system default is
    // this fixes some edge cases with special characters not displaying correctly
    // see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
    // If Javadoc is generated, this must be specified in that task too.
    it.options.encoding = "UTF-8"
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        it.options.release = targetJavaVersion
    }
}

java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
    archivesBaseName = project.archives_base_name
    withSourcesJar()
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.archivesBaseName}" }
    }
    manifest {
        attributes([
                'Specification-Title'     : mod_name,
                'Specification-Vendor'    : mod_author,
                'Specification-Version'   : mod_version,
                'Implementation-Title'    : project.name,
                'Implementation-Version'  : mod_version,
                'Implementation-Vendor'   : mod_author,
                'Implementation-Timestamp': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                'Timestampe'              : System.currentTimeMillis(),
                'Built-On-Java'           : "${System.getProperty('java.vm.version')} (${System.getProperty('java.vm.vendor')})",
                'Build-On-Minecraft'      : minecraft_version
        ])
    }
}

// configure the maven publication
publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = "https://maven.pkg.github.com/sib-energy-craft/sib-energy-craft"
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_ACTOR")
                password = project.findProperty("gpr.key") ?: System.getenv("GITHUB_TOKEN")
            }
        }
    }
    publications {
        gpr(MavenPublication) {
            from(components.java)
        }
    }
}
